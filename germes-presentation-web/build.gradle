plugins {
    id 'war'
    id "org.gretty"
    id "com.moowork.node" version "1.3.1"
}

apply plugin: "com.moowork.node"

dependencies {
    implementation project(':germes-presentation-rest')

    runtimeOnly persistence.mysql
}

gretty {
    servletContainer = 'jetty9.4'
    contextPath = '/'
    httpPort = 8080
}

node {
    nodeModulesDir = file("client")
}

task cleanAngular(type:Delete) {
    delete "${projectDir}/client/dist"
}

task testAngular(type:NpmTask) {
    args = ['run', 'testExit']
}

task buildAngular(type:NpmTask, dependsOn:'npmInstall') {
    args = ['run', 'build']
}

clean.dependsOn 'cleanAngular'
test.dependsOn 'testAngular'
assemble.dependsOn 'buildAngular'

war {
    archiveName 'web.war'
    from 'client/dist'
}