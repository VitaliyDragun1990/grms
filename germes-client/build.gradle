plugins {
    id "org.gretty"
    id "com.moowork.node" version "1.3.1"
}

if (!project.hasProperty('skipAngular')) {
    apply plugin: "com.moowork.node"

    node {
        nodeModulesDir = file("client")
    }

    tasks.register("createNodeModulesDir") {
        doFirst { mkdir node.nodeModulesDir }
    }

    yarn_install.mustRunAfter createNodeModulesDir

    tasks.register("cleanAngular", Delete) {
        doFirst { delete "${projectDir}/client/dist" }
    }

    tasks.register("testAngular", CacheableYarnTask) {
        args = ['run', 'testExit']
    }

    tasks.register("buildAngular", CacheableYarnTask) {
        dependsOn tasks.named("yarn_install")
        args = ['run', "build"]
    }

    clean.dependsOn tasks.named("cleanAngular")
    test.dependsOn tasks.named("testAngular")
    assemble.dependsOn tasks.named("buildAngular")
}

import com.moowork.gradle.node.yarn.YarnTask

@CacheableTask
class CacheableYarnTask extends YarnTask {

    @InputFiles
    FileCollection sourceFiles = project.files('client/src', 'client/package.json', 'client/.angular-cli.json')

    @OutputDirectory
    File outputDir = project.file("client/dist");
}